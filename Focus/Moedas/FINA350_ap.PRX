//Programa - Criar por Andre Salgado / Introde
//Melhoria - Utilizado para atender as melhorias solicitadas pela equipe
//Utilizado os detalhes do programa padrão Totvs - FINA350, pois conforme solicitação Evelyn, 
// precisa ter opção para gerar em qualquer momento a variação sem considerar a data base travada no usuario conforme regra
//Autor - Andre / Introde - 22/09/2015
//Solicitação - Evelyn/FTI
#include "rwmake.ch"
//#INCLUDE "PROTHEUS.CH"

User Function FINA350_i()

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ FINA350    ³ Autor ³ Pilar S. Albaladejo ³ Data ³ 26.03.95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Lancamentos Correcao Monetaria                             ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Sintaxe   ³ FINA350()                                                  ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³Parametros³                                                            ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ SIGAFIN                                                    ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³   DATA   ³ PROGRAMADOR   ³MANUTENCAO EFETUADA    ³BOPS:   00000136412 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ 21/11/07 ³ Pedro P Lima  ³ A rotina nao estava buscando os valores das³±±
±±³          ³    TI6434     ³ contas de debito quando o LP 598 era       ³±±
±±³          ³               ³ para utilizar como valor o campo           ³±±
±±³          ³               ³ SA1->A1_CONTA. Quando era executada havendo³±±
±±³          ³               ³ dois ou mais clientes o valor da Conta     ³±±
±±³          ³               ³ Debito do primeiro cliente na verdade      ³±±
±±³          ³               ³ apresentava o valor da conta debito do     ³±±
±±³          ³               ³ segundo cliente, e o segundo aparecia em   ³±±
±±³          ³               ³ branco.                                    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
//Static Local lPanelFin := If (FindFunction("IsPanelFin"),IsPanelFin(),.F.)	//Controle de Paniel, nao vamos utilizar neste Projeto!
LOCAL nOpca := 0
Local aSays	:={}, aButtons:={}
Local lCont	:= .T.
Private cCadastro := OemToAnsi("Calculo da Variação Cambial")

AjustaSX1()
Pergunte("AFI350",.F.)


//Variaveis
dDataCalc	:= ctod("  /  /  ")				// Data de Calculo
cTipoV 		:= ""							// Seleção da Taxa
//vItems		:= {"1=Banco Central","2=Corporation"}//Lista das Taxa
vItems		:= {"1=Banco Central"}//Lista das Taxa
cMoedaDE	:= 0
cMoedaATE	:= 0

//Pre-Tela - Adicional do Calculo da Variação Cambial
Set Device To Screen
oNota := NIL
@ 247,182 To 483,518 Dialog oNota Title OemToAnsi("Parametros para Consulta")
@ 005,003 SAY "Data de Calculo Variação ?"
@ 005,075 GET dDataCalc valid !Empty(dDataCalc)  size 50,50

@ 025,003 SAY "Utilizar a Taxa ?"
@ 025,075 COMBOBOX cTipoV ITEMS vItems SIZE 50,50

@ 045,003 SAY "Moeda De "
@ 045,075 GET cMoedaDE 	Picture "@E 99" size 50,50  //F3 "SZM"

@ 065,003 SAY "Moeda Ate"
@ 065,075 GET cMoedaATE Picture "@E 99" size 50,50  //F3 "SZM"

@ 087,130 BmpButton Type 1 Action (close(onota))

Activate Dialog oNota Centered
Set Device To Print


//Monta Taxa
m->Mv_PAR08 := IF(cTipoV="2", Posicione("SM2",1,dtos(dDataCalc),"M2_MOEDA4"), RecMoeda(dDataCalc,mv_par02) )
m->Mv_PAR09 := IF(cTipoV="2", Posicione("SM2",1,dtos(dDataCalc),"M2_MOEDA4"), RecMoeda(dDataCalc,mv_par02) )


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicializa o log de processamento                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ProcLogIni( aButtons )

AADD(aSays,OemToAnsi( "O objetivo deste programa consiste em efetuar a  apura‡„o  da diferen‡a" ) ) //"O objetivo deste programa consiste em efetuar a  apura‡„o  da diferen‡a"
AADD(aSays,OemToAnsi( "entre a data de emiss„o e data base dos t¡tulos em aberto em moeda for-") ) //"entre a data de emiss„o e data base dos t¡tulos em aberto em moeda for-"
AADD(aSays,OemToAnsi( "te. A diferen‡a ser  lan‡ada na contabilidade (Varia‡„o Monet ria)." ) ) //"te. A diferen‡a ser  lan‡ada na contabilidade (Varia‡„o Monet ria)."

/*
If lPanelFin  //Chamado pelo Painel Financeiro
aButtonTxt := {}
If Len(aButtons) > 0
AADD(aButtonTxt,{"Visualizar","Visualizar",aButtons[1][3]}) // Visualizar
Endif
AADD(aButtonTxt,{"Parametros","Parametros", {||F350BTOP()}}) // Parametros
FaMyFormBatch(aSays,aButtonTxt,{||nOpca:=1},{||nOpca:=0})
Else
AADD(aButtons, { 5,.T.,{|| Pergunte("AFI350",.T. ) } } )
AADD(aButtons, { 1,.T.,{|o| nOpca:= 1,o:oWnd:End()}} )
AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )
FormBatch( cCadastro, aSays, aButtons,,,450 )
Endif
*/
AADD(aButtons, { 5,.T.,{|| Pergunte("AFI350",.T. ) } } )
AADD(aButtons, { 1,.T.,{|o| nOpca:= 1,o:oWnd:End()}} )
AADD(aButtons, { 2,.T.,{|o| o:oWnd:End() }} )
FormBatch( cCadastro, aSays, aButtons,,,450 )


Private nMoeda
Private lSkip

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Variaveis utilizadas para parametros                          ³
//³ MV_PAR01 - Carteira -> Receber/Pagar/Ambas                    ³
//³ MV_PAR02 - Moeda?                                             ³
//³ MV_PAR03 - Mostra lancamento contabil?                        ³
//³ MV_PAR04 - Aglutina?                                          ³
//³ MV_PAR05 - Contabliza por Clinte/Fornecedor ou Titulo         ³
//³ MV_PAR06 - Contabiliza Variacao de Taxa Contratada ?          ³
//| MV_PAR07 - Considera titulos com emissao futura ?			      |
//| MV_PAR08 - Informe a taxa de venda da moeda             	   |
//| MV_PAR09 - Informe a taxa de venda da compra             	   |
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ

//If ExistBlock("F350CONF")
//	lCont:= ExecBlock("F350CONF",.F.,.F.)
//Endif

If nOpcA == 1 .And. lCont
	//	nMoeda := mv_par02
	
	//Melhoria para Calculo Automatico para Todos as Moedas Selecionadas dentro do Periodo
	For nX := cMoedaDE to cMoedaATE
		
		nMoeda := nX
		//Nome
		If nMoeda = 2
			cNomMoed := "Dólar Americano Compra"
		ElseIf nMoeda = 3
			cNomMoed := "Dólar Americano Venda"
		ElseIf nMoeda = 4
			cNomMoed := "Euro Compra"
		ElseIf nMoeda = 5
			cNomMoed := "Euro Vendas"
		ElseIf nMoeda = 6
			cNomMoed := "Iene Compra"
		ElseIf nMoeda = 7
			cNomMoed := "Iene Venda"
		ElseIf nMoeda = 8
			cNomMoed := "Dólar Canadense Compra"
		ElseIf nMoeda = 9
			cNomMoed := "Dólar Canadense Venda"
		ElseIf nMoeda = 10
			cNomMoed := "Dólar Hong Kong Compra "
		ElseIf nMoeda = 11
			cNomMoed := "Dólar Hong Kong Venda"
		ElseIf nMoeda = 12
			cNomMoed := "Dólar Australiano Compra"
		ElseIf nMoeda = 13
			cNomMoed := "Dólar Australiano Venda"
		ElseIf nMoeda = 14
			cNomMoed := "Libra Esterlina Compra"
		ElseIf nMoeda = 15
			cNomMoed := "Libra Esterlina Venda"
		ElseIf nMoeda = 16
			cNomMoed := "Dólar Cingapura Compra"
		ElseIf nMoeda = 17
			cNomMoed := "Dólar Cingapura Venda"
		Endif
		
//		Msginfo("Sera feito o Calculo da Moeda "+transform(nMoeda,"@E 99")+" "+cNomMoed)
		mv_par02 := nMoeda
		
		Processa({|lEnd| FA350Calc()})  // Chamada da funcao de contabiliza‡Æo
		
	Next nX

Endif

/*
If lPanelFin //Chamado pelo Painel Financeiro
dbSelectArea(FinWindow:cAliasFile)
ReCreateBrow(FinWindow:cAliasFile,FinWindow)
Endif
*/

Return

/*
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³ Fa350Calc  ³ Autor ³ Pilar S. Albaladejo   ³ Data ³ 25/03/95 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Calculo da variacao dos titulos                              ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´±±
±±³ Uso      ³ FINA350                                                      ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
*/
Static Function FA350Calc()

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Definicao das variaveis a serem utilizadas pela rotina              ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
Local nHdlPrv
Local cArquivo
Local cPadrao
Local nTotal     := 0
Local lHeadProva  := .F.
Local lPadrao
Local nValor1	:= 0
Local nValorM	:= 0
Local lF350Cm	:= ExistBlock("F350CM")
Local lF350Cm2	:= ExistBlock("F350CM2")
Local lF350SE1	:= ExistBlock("F350SE1")
Local lF350SE2	:= ExistBlock("F350SE2")
Local lF350GE1	:= ExistBlock("F350GE1")
Local lF350GE2	:= ExistBlock("F350GE2")
Local nTxVenda	:= IF(cTipoV="2", Posicione("SM2",1,dtos(dDataCalc),"M2_MOEDA4"), RecMoeda(dDataCalc,mv_par02) ) //Iif (MV_PAR08 > 0,MV_PAR08,RecMoeda(dDataCalc,MV_PAR02))
Local nTxCompra	:= IF(cTipoV="2", Posicione("SM2",1,dtos(dDataCalc),"M2_MOEDA4"), RecMoeda(dDataCalc,mv_par02) ) //Iif (MV_PAR09 > 0,MV_PAR09,RecMoeda(dDataCalc,MV_PAR02))
Local cAliasSE1
Local cAliasSE2
Local aFlagCTB 	:= {}
Local lUsaFlag	:= SuperGetMV( "MV_CTBFLAG" , .T. /*lHelp*/, .F. /*cPadrao*/)
Local lCNTA100  := SuperGetMV("MV_CNFI350",.F.,.F.)

#IFDEF TOP
	Local cWhere := ""
#ENDIF

Private aRotina := {{"", "", 0, 1}, ;
{"", "", 0, 2}, ;
{"", "", 0, 3}, ;
{"", "", 0, 4} }

Private cLote


VALOR := 0

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Verifica o numero do Lote                                    ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
LoteCont("FIN")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza o log de processamento   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ProcLogAtu("INICIO")

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicia calculo do SE1                                            ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If mv_par01 == 1 .Or. mv_par01 == 3
	
	ProcRegua(RecCount())
	
	#IFDEF TOP
		cAliasSE1 := GetNextAlias()
		
		If MV_PAR06 == 2
			cWhere := "E1_TXMOEDA = 0 AND "
		Endif
		If MV_PAR07 == 2
			cWhere += "E1_EMISSAO <= '"+DTOS(dDataCalc)+"' AND "
		EndIf
		cWhere += "E1_MOEDA = "+Alltrim(Str(mv_par02))+" AND "
		cWhere += "(E1_TIPO NOT IN " + FORMATIN(MVPROVIS+"|"+MVABATIM,"|")
		
		//-- Parametro para realizaar contab. var. cambial dos titulos provisorios do SIGAGCT
		If lCNTA100
			cWhere += " OR E1_ORIGEM = 'CNTA100'"
		EndIf
		cWhere += ") "
		cWhere := "%"+cWhere+"%"
		
		BeginSql alias cAliasSE1
			SELECT E1_FILIAL, E1_PREFIXO, E1_NUM, E1_PARCELA, E1_TIPO, E1_CLIENTE, E1_LOJA, E1_MOEDA, E1_SALDO, E1_TXMOEDA, E1_EMISSAO, E1_DTVARIA, E1_VALOR, E1_VLCRUZ, E1_EMISSAO, E1_NATUREZ, SE1.R_E_C_N_O_ RECNOE1
			FROM %table:SE1% SE1
			WHERE E1_FILIAL = %xFilial:SE1% AND
			E1_SALDO > 0 AND
			%Exp:cWhere% AND
			SE1.%NotDel%
			ORDER BY %Order:SE1%
		EndSql
		
	#ELSE
		cAliasSe1 := "SE1"
		dbSelectArea("SE1")
		dbSetOrder(2)
		dbSeek(cFilial)
	#ENDIF
	While (cAliasSE1)->(!Eof()) .and. (cAliasSE1)->E1_FILIAL == xFilial("SE1")
		
		IncProc()
		
		cCliente:= E1_Cliente
		cLoja   := E1_Loja
		lSkip	:= .F.
		
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Acumula a variacao de todos os titulos do cliente atual           ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		While (cAliasSE1)->(!Eof()) .And.  (cAliasSE1)->E1_Filial   == xFilial("SE1") .And. ;
			(cAliasSE1)->E1_Cliente == cCliente .And. ;
			(cAliasSE1)->E1_Loja    == cLoja
			
			#IFDEF TOP
				SE1->(MsGOTO((cAliasSE1)->RECNOE1))
			#ENDIF
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza os titulos que nao satisfacam as condicoes do programa ³
			//³ 1. Emitido na moeda diferente                                   ³
			//³ 2. Ja baixado                                                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			IF SE1->E1_MOEDA != nMoeda .or. SE1->E1_SALDO == 0
				(cAliasSE1)->(dbSkip())
				Loop
			EndIf
			
			If MV_PAR06 == 2 .And. SE1->E1_TXMOEDA <> 0
				(cAliasSE1)->(dbSkip())
				Loop
			EndIf
			
			IF SE1->E1_TIPO $ MVPROVIS .or. SE1->E1_TIPO $ MVABATIM
				If AllTrim(SE1->E1_ORIGEM) #"CNTA100" .Or. !lCNTA100
					(cAliasSE1)->(dbSkip())
					Loop
				EndIf
			Endif
			
//*** Validação - Nao pode gerar o mesmo dia
			If dDataCalc = SE1->E1_DTVARIA
				(cAliasSE1)->(dbSkip())
				Loop
			EndIf

			
			// Se nao considera titulos com emissao futura, ignora o titulo se ele estiver com emissao futura
			If MV_PAR07 == 2 .And. SE1->E1_EMISSAO > dDataCalc
				(cAliasSE1)->(dbSkip())
				Loop
			EndIf
			
			If lF350SE1
				If !(ExecBlock("F350SE1",.F.,.F.))
					(cAliasSE1)->(dbSkip())
					Loop
				EndIf
			EndIf
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calcula o valor na moeda escolhida na data da ultima variacao   ³
			//³ ou na data de emissao (caso nao tenha sofrido ainda nenhuma     ³
			//³ variacao.                                                       |
			//³ Calcula o valor na moeda escolhida para a database.             ³
			//³ Subtrai um valor do outro para apurar a variacao.               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//Caso seja a primeira apuracao de variacao monetaria
			If Empty(SE1->E1_TXMOEDA) .and. Empty(SE1->E1_DTVARIA) .and. STR(SE1->E1_SALDO,17,2) == STR(SE1->E1_VALOR,17,2)
				nValor1 := SE1->E1_VLCRUZ
			Else
				If(SE1->(FieldPos("E1_TXMDCOR")>0 ) .And. !Empty(SE1->E1_TXMDCOR))
					nValor1 := xMoeda(SE1->E1_SALDO,SE1->E1_MOEDA,1,Iif(Empty(SE1->E1_DTVARIA),SE1->E1_EMISSAO,SE1->E1_DTVARIA),TamSX3("E1_TXMDCOR")[2],SE1->E1_TXMDCOR)
				Else
					nValor1 := xMoeda(SE1->E1_SALDO,SE1->E1_MOEDA,1,Iif(Empty(SE1->E1_DTVARIA),SE1->E1_EMISSAO,SE1->E1_DTVARIA),TamSX3("E1_TXMDCOR")[2],Iif(Empty(SE1->E1_DTVARIA),SE1->E1_TXMOEDA,0))
				EndIf
			Endif
			
			nValorM  :=  xMoeda(SE1->E1_SALDO,SE1->E1_MOEDA,1,dDataCalc,,nTxVenda)
			
			If lF350CM
				VALOR += Execblock("F350CM",.F.,.F.)
			Else
				VALOR += (nValorM - nValor1)
			Endif
			
			If VALOR != 0
				RecLock("SE1")
				Replace E1_DTVARIA With dDataCalc
				If SE1->(FieldPos("E1_TXMDCOR"))>0
					Replace E1_TXMDCOR with nTxVenda
				EndIf
				MsUnlock()
				
				If lF350GE1
					ExecBlock ("F350GE1",.F.,.F.,VALOR)
				Endif
				
			Endif
			
			If mv_par05 == 2		//contabiliza por titulo
				lSkip := .T.
				Exit
			Endif
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Passa para o proximo titulo                                     ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			(cAliasSE1)->(dbSkip())
		Enddo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Contabiliza para o cliente verificado                             ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If VALOR != 0
			dbSelectArea("SED")
			dbSeek(xFilial("SED")+SE1->E1_NATUREZ)
			dbSelectArea("SA1")
			SA1->(MsSeek(FWxFilial("SA1",SE1->E1_FILORIG)+SE1->(E1_CLIENTE+E1_LOJA)))
			
			If mv_par05 == 2
				Reclock("SE5",.T.)
				Replace E5_FILIAL With xFilial()
				Replace E5_PREFIXO With SE1->E1_PREFIXO
				Replace E5_NUMERO  With SE1->E1_NUM
				Replace E5_PARCELA With SE1->E1_PARCELA
				Replace E5_TIPO    With SE1->E1_TIPO
				Replace E5_CLIFOR  With SE1->E1_CLIENTE
				Replace E5_LOJA    With SE1->E1_LOJA
				Replace E5_VALOR   With VALOR
				Replace E5_VLMOED2 With xMoeda(VALOR,1,SE1->E1_MOEDA)
				Replace E5_DATA    With dDataCalc
				Replace E5_NATUREZ With SE1->E1_NATUREZ
				Replace E5_RECPAG  With "R"
				Replace E5_TIPODOC With "VM"
				If !lUsaFlag
					Replace E5_LA      With "S"
				Endif
				Replace E5_DTDIGIT With dDataCalc
				Replace E5_DTDISPO With dDataCalc
				Replace E5_HISTOR  With "CORREC MONET."
				Replace E5_TXMOEDA With nTxVenda
				MsUnlock()
			Endif
			
			cPadrao   := "598"
			lPadrao   := VerPadrao(cPadrao)
			IF lPadrao
				IF !lHeadProva
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Inicializa Lancamento Contabil                                   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					nHdlPrv := HeadProva( cLote,;
					"FINA350" /*cPrograma*/,;
					Substr( cUsuario, 7, 6 ),;
					@cArquivo )
					
					lHeadProva := .T.
				End
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Prepara Lancamento Contabil                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil
					// aAdd( aFlagCTB, {"E5_LA", "S", "SE5", SE5->( Recno() ), 0, 0, 0} )
				Endif
				nTotal += DetProva( nHdlPrv,;
				cPadrao,;
				"FINA350" /*cPrograma*/,;
				cLote,;
				/*nLinha*/,;
				/*lExecuta*/,;
				/*cCriterio*/,;
				/*lRateio*/,;
				/*cChaveBusca*/,;
				/*aCT5*/,;
				/*lPosiciona*/,;
				@aFlagCTB,;
				/*aTabRecOri*/,;
				/*aDadosProva*/ )
				
			Endif
		Endif
		If mv_par05 == 2
			// por titulo, vai para o proximo registro
			If lSkip
				(cAliasSE1)->(dbSkip())
			Endif
		Endif
		VALOR := 0
	Enddo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Volta o arquivo para a chave primaria                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP
		(cAliasSE1)->(dbCloseArea())
		cWhere := ""
	#ENDIF
	dbSelectArea("SE1")
	dbSetOrder(1)
	
	VALOR    := 0
Endif


//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Inicia calculo do SE2                                         ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
If mv_par01 == 2 .or. mv_par01 == 3
	ProcRegua(RecCount())
	#IFDEF TOP
		
		cAliasSE2 := GetNextAlias()
		
		If MV_PAR06 == 2
			cWhere := "E2_TXMOEDA = 0 AND "
		Endif
		If MV_PAR07 == 2
			cWhere += "E2_EMISSAO <= '"+DTOS(dDataCalc)+"' AND "
		EndIf
		cWhere += "E2_MOEDA = "+Alltrim(Str(mv_par02))+" AND "
		cWhere += "(E2_TIPO NOT IN " + FORMATIN(MVPROVIS+"|"+MVABATIM,"|")
		//-- Parametro para realizaar contab. var. cambial dos titulos provisorios do SIGAGCT
		If lCNTA100
			cWhere += " OR E2_ORIGEM = 'CNTA100'"
		EndIf
		cWhere += ") "
		cWhere := "%"+cWhere+"%"
		
		BeginSql alias cAliasSE2
			SELECT E2_FILIAL, E2_PREFIXO, E2_NUM, E2_PARCELA, E2_TIPO, E2_FORNECE, E2_LOJA, E2_MOEDA, E2_SALDO, E2_TXMOEDA, E2_EMISSAO, E2_DTVARIA, E2_VALOR, E2_VLCRUZ, E2_NATUREZ, SE2.R_E_C_N_O_ RECNOE2
			FROM %table:SE2% SE2
			WHERE E2_FILIAL = %xFilial:SE2% AND
			E2_SALDO > 0 AND
			%Exp:cWhere% AND
			SE2.%NotDel%
			ORDER BY %Order:SE2%
		EndSql
		
	#ELSE
		cAliasSE2 := "SE2"
		dbSelectArea("SE2")
		dbSetOrder(6)
		dbSeek(xFilial("SE2"))
	#ENDIF
	
	While (cAliasSE2)->(!Eof()) .and. (cAliasSE2)->E2_FILIAL == xFilial("SE2")
		
		IncProc()
		
		cFornece := E2_Fornece
		cLoja    := E2_Loja
		lSkip := .F.
		
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Acumula a variacao de todos os titulos do fornecedor atual        ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		While (cAliasSE2)->(!Eof()) .And.   (cAliasSE2)->E2_Filial   == xFilial("SE2")  .And. ;
			(cAliasSE2)->E2_Fornece  == cFornece .And. ;
			(cAliasSE2)->E2_Loja     == cLoja
			
			#IFDEF TOP
				SE2->(MsGOTO((cAliasSE2)->RECNOE2))
			#ENDIF
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Despreza os titulos que nao satisfacam as condicoes do programa³
			//³ 1. Emitido na moeda solicitada                                 ³
			//³ 2. Ja baixado                                                  ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			IF SE2->E2_MOEDA != nMoeda .or. SE2->E2_SALDO = 0
				(cAliasSE2)->(dbSkip( ))
				Loop
			Endif
			
			IF SE2->E2_TIPO $ MVPROVIS .or. SE2->E2_TIPO $ MVABATIM
				If AllTrim(SE2->E2_ORIGEM) #"CNTA100" .Or. !lCNTA100
					(cAliasSE2)->(dbSkip( ))
					Loop
				EndIf
			Endif
			
			If MV_PAR06 == 2 .And. SE2->E2_TXMOEDA <> 0
				(cAliasSE2)->(dbSkip())
				Loop
			EndIf


//*** Validação - Nao pode gerar o mesmo dia
			If dDataCalc = SE2->E2_DTVARIA
				(cAliasSE2)->(dbSkip())
				Loop
			EndIf

			
			// Se nao considera titulos com emissao futura, ignora o titulo se ele estiver com emissao futura
			If MV_PAR07 == 2 .And. SE2->E2_EMIS1 > dDataCalc
				(cAliasSE2)->(dbSkip())
				Loop
			EndIf
			
			If lF350SE2
				If !(ExecBlock("F350SE2",.F.,.F.))
					(cAliasSE2)->(dbSkip())
					Loop
				EndIf
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Calcula o valor na moeda escolhida na data da ultima variacao   ³
			//³ ou na data de emissao (caso nao tenha sofrido ainda nenhuma     ³
			//³ variacao.                                                       ³
			//³ Calcula o valor na moeda escolhida para a database.             ³
			//³ Subtrai um valor do outro para apurar a variacao.               ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			//Caso seja a primeira apuracao de variacao monetaria
			If Empty(SE2->E2_TXMOEDA) .and. Empty(SE2->E2_DTVARIA) .and. STR(SE2->E2_SALDO,17,2) == STR(SE2->E2_VALOR,17,2)
				nValor1 := SE2->E2_VLCRUZ
			Else
				If(SE2->(FieldPos("E2_TXMDCOR")>0 ) .And. !Empty(SE2->E2_TXMDCOR))
					nValor1 := xMoeda(SE2->E2_SALDO,SE2->E2_MOEDA,1,Iif(Empty(SE2->E2_DTVARIA),SE2->E2_EMISSAO,SE2->E2_DTVARIA),TamSX3("E2_TXMDCOR")[2],SE2->E2_TXMDCOR)
				Else
					nValor1 := xMoeda(SE2->E2_SALDO,SE2->E2_MOEDA,1,Iif(Empty(SE2->E2_DTVARIA),SE2->E2_EMISSAO,SE2->E2_DTVARIA),TamSX3("E2_TXMDCOR")[2],Iif(Empty(SE2->E2_DTVARIA),SE2->E2_TXMOEDA,0))
				EndIf
			Endif
			
			nValorM  :=    xMoeda(SE2->E2_SALDO,SE2->E2_MOEDA,1,dDataCalc,TamSX3("E2_TXMDCOR")[2],nTxCompra)
			
			If lF350CM2
				VALOR += Execblock("F350CM2",.F.,.F.)
			Else
				VALOR += (nValorM - nValor1)
			Endif
			
			If VALOR != 0
				RecLock("SE2")
				Replace E2_DTVARIA With dDataCalc
				If SE2->(FieldPos("E2_TXMDCOR")>0)
					Replace E2_TXMDCOR With nTxCompra
				EndIf
				MsUnlock()
				
				If lF350GE2
					ExecBlock ("F350GE2",.F.,.F.,VALOR)
				Endif
				
			Endif
			
			If mv_par05 == 2			// contabiliza por titulo
				lSkip := .T.
				Exit
			Endif
			
			//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
			//³ Passa para o proximo titulo                                   ³
			//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
			(cAliasSE2)->(dbSkip())
		Enddo
		
		//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
		//³ Contabiliza para o fornecedor verificado                          ³
		//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
		If VALOR != 0
			dbSelectArea("SED")
			dbSeek(xFilial("SED")+SE2->E2_NATUREZ)
			dbSelectArea("SA2")
			dbSeek(FWxFilial("SA2",SE2->E2_FILORIG)+SE2->(E2_FORNECE+E2_LOJA))
			
			If mv_par05 == 2
				Reclock("SE5",.T.)
				Replace E5_FILIAL With xFilial()
				Replace E5_PREFIXO With SE2->E2_PREFIXO
				Replace E5_NUMERO  With SE2->E2_NUM
				Replace E5_PARCELA With SE2->E2_PARCELA
				Replace E5_TIPO    With SE2->E2_TIPO
				Replace E5_CLIFOR  With SE2->E2_FORNECE
				Replace E5_LOJA    With SE2->E2_LOJA
				Replace E5_VALOR   With VALOR
				Replace E5_VLMOED2 With xMoeda(VALOR,1,SE2->E2_MOEDA)
				Replace E5_DATA    With dDataCalc
				Replace E5_NATUREZ With SE2->E2_NATUREZ
				Replace E5_RECPAG  With "P"
				Replace E5_TIPODOC With "VM"
				If !lUsaFlag
					Replace E5_LA With "S"
				Endif
				Replace E5_DTDIGIT With dDataCalc
				Replace E5_DTDISPO With dDataCalc
				Replace E5_HISTOR  With "CORREC MONET."
				Replace E5_TXMOEDA With nTxVenda
				
				MsUnlock()
			Endif
			
			cPadrao:="599"
			lPadrao:=VerPadrao(cPadrao)
			IF lPadrao
				IF !lHeadProva
					SA2->(DbSetOrder(1))
					SA2->(dbSeek( xFilial("SA2") + cFornece+cLoja))
					//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
					//³ Inicializa Lancamento Contabil                                   ³
					//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
					nHdlPrv := HeadProva( cLote,;
					"FINA350" /*cPrograma*/,;
					Substr( cUsuario, 7, 6 ),;
					@cArquivo )
					lHeadProva := .T.
				End
				//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
				//³ Prepara Lancamento Contabil                                      ³
				//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
				If lUsaFlag  // Armazena em aFlagCTB para atualizar no modulo Contabil
					// aAdd( aFlagCTB, {"E5_LA", "S", "SE5", SE5->( Recno() ), 0, 0, 0} )
				Endif
				nTotal += DetProva( nHdlPrv,;
				cPadrao,;
				"FINA350" /*cPrograma*/,;
				cLote,;
				/*nLinha*/,;
				/*lExecuta*/,;
				/*cCriterio*/,;
				/*lRateio*/,;
				/*cChaveBusca*/,;
				/*aCT5*/,;
				/*lPosiciona*/,;
				@aFlagCTB,;
				/*aTabRecOri*/,;
				/*aDadosProva*/ )
			Endif
		Endif
		If mv_par05 == 2
			// por titulo, vai para o proximo registro
			If lSkip
				(cAliasSE2)->(dbSkip())
			Endif
		Endif
		VALOR := 0
	Enddo
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Volta o arquivo para a chave primaria                            ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	#IFDEF TOP
		(cAliasSE2)->(dbCloseArea())
	#ENDIF
	dbSelectArea("SE2")
	dbSetOrder(1)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Se houve variacao serao efetuados lancamentos contabeis ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
IF lHeadProva
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Chama tela de lanc contabeis para usuario confirmar          ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	lDigita  :=Iif(mv_par03==1,.T.,.F.)
	lAglutina:=Iif(mv_par04==1,.T.,.F.)
	
	//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
	//³ Efetiva Lan‡amento Contabil                                      ³
	//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
	RodaProva( nHdlPrv,;
	nTotal)
	
	cA100Incl( cArquivo,;
	nHdlPrv,;
	3 /*nOpcx*/,;
	cLote,;
	lDigita,;
	lAglutina,;
	/*cOnLine*/,;
	dDataCalc,;
	/*dReproc*/,;
	@aFlagCTB,;
	/*aDadosProva*/,;
	/*aDiario*/ )
	aFlagCTB := {}  // Limpa o coteudo apos a efetivacao do lancamento
	
Endif

If nHdlPrv != NIL
	FClose(nHdlPrv)
Endif

//ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿
//³ Atualiza o log de processamento   ³
//ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ
ProcLogAtu("FIM")

Return NIL

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Fun‡…o    ³F350VlPerg() ³ Autor ³ Paulo Augusto    ³ Data ³ 16.05.05 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ajusta Pergunta Moedas ?       				    	    ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function F350VlPerg()()

Local lRet:= .F.

If mv_par02 > 1 .and. mv_par02 <= MoedFin()
	lRet:= .T.
EndIf

If lRet
	M->Mv_PAR08 := IF(cTipoV="2", Posicione("SM2",1,dtos(dDataCalc),"M2_MOEDA4"), RecMoeda(dDataCalc,mv_par02) ) //RecMoeda(dDataCalc,m->mv_par02)
	M->Mv_PAR09 := IF(cTipoV="2", Posicione("SM2",1,dtos(dDataCalc),"M2_MOEDA4"), RecMoeda(dDataCalc,mv_par02) ) //RecMoeda(dDataCalc,m->mv_par02)
EndIf

Return (lRet)

/*/
ÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜÜ
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
±±ÚÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÂÄÄÄÄÄÄÂÄÄÄÄÄÄÄÄÄÄ¿±±
±±³Programa  ³ AjustaSX1³ Autor ³ Gustavo Henrique      ³ Data ³ 06/11/07 ³±±
±±ÃÄÄÄÄÄÄÄÄÄÄÅÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄ´±±
±±³Descri‡…o ³ Ajusta pergunta mv_par02 "Moeda"                           ³±±
±±ÀÄÄÄÄÄÄÄÄÄÄÁÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ±±
±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±±
ßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßßß
/*/
Static Function AjustaSX1()

SX1->( dbSetOrder(1) )

// Pesquisa pergunta de moeda como COMBO. Se encontrar, altera para GET que a forma correta
If	SX1->( MsSeek( PadR( "AFI350", Len(SX1->X1_GRUPO) ) + "02" ) ) .And.;
	SX1->X1_GSC == "C"
	
	RecLock("SX1",.F.)
	SX1->X1_GSC		:= "G"	// Altera para GET
	SX1->X1_CNT01	:= "2"
	SX1->X1_DEF01	:= ""
	SX1->X1_DEFSPA1	:= ""
	SX1->X1_DEFENG1	:= ""
	SX1->X1_DEF02	:= ""
	SX1->X1_DEFSPA2	:= ""
	SX1->X1_DEFENG2	:= ""
	SX1->X1_DEF03	:= ""
	SX1->X1_DEFSPA3	:= ""
	SX1->X1_DEFENG3	:= ""
	SX1->X1_DEF04	:= ""
	SX1->X1_DEFSPA4	:= ""
	SX1->X1_DEFENG4	:= ""
	SX1->(MsUnlock())
	
EndIf

If	SX1->( MsSeek( PadR( "AFI350", Len(SX1->X1_GRUPO) ) + "09" ) )
	If "VENTA" $ AllTrim( Upper( SX1->X1_PERSPA ) )
		RecLock("SX1",.F.)
		SX1->X1_PERSPA := "¿Tasa de compra de la moneda ?"
		SX1->(MsUnlock())
	EndIf
	If "SELL" $ AllTrim( Upper( SX1->X1_PERENG ) )
		RecLock("SX1",.F.)
		SX1->X1_PERENG := "Currency purchase rate ?"
		SX1->(MsUnlock())
	EndIf
EndIf

Return Nil


Static Function F350BTOP()
Pergunte("AFI350",.T. )
LimpaMoeda()
Return
